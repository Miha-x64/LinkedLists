apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 32
    defaultConfig {
        applicationId 'net.aquadc.linkedlists'
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 2
        versionName '0.1'
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    
    signingConfigs {
        release {
            storeFile file("${System.getProperty('user.home')}/.android/debug.keystore")
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    flavorDimensions 'server'
    productFlavors {
        hosted {
            dimension 'server'
            buildConfigField('String', 'SERVER', '"http://lab.iamrohit.in/php_ajax_country_state_city_dropdown/"')
            buildConfigField('String', 'PATH', '"apiv1.php"')
        }
        local {
            dimension 'server'
            buildConfigField('String', 'SERVER', '"http://192.168.0.103:8080"')
            buildConfigField('String', 'PATH', 'net.aquadc.linkedlists.CommonKt.LOCAL_PATH')
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    packagingOptions {
        exclude 'META-INF/*kotlin*'
        exclude 'META-INF/kotlin*'
        exclude 'kotlin'
        exclude 'kotlin/*'
        exclude 'kotlin/**'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation "com.louiscad.splitties:splitties-views-dsl:$splitties_version"
    implementation "com.louiscad.splitties:splitties-systemservices:$splitties_version"
    implementation "su.lychee:android-bindings:$lychee_version"
    implementation "su.lychee:android-json:$lychee_version"
    implementation "su.lychee:properties:$lychee_version"
    implementation "su.lychee:extended-persistence:$lychee_version" // TokenStream.entries transform
    implementation(project(':common'))

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
